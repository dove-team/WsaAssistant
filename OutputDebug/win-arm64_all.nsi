; Script generated by the HM NIS Edit Script Wizard.
; help -- https://blog.csdn.net/u012896330/article/details/54949024
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "WsaAssistant"
!define PRODUCT_VERSION "1.5.2"
!define PRODUCT_PUBLISHER "michael_eddy"
!define PRODUCT_WEB_SITE "https://michael-eddy.github.io"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\WsaAssistant.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "LogicLib.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\WsaAssistant\images\icon.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\WsaAssistant.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Russian"
!insertmacro MUI_LANGUAGE "SimpChinese"
!insertmacro MUI_LANGUAGE "TradChinese"

Section "-LogSetOn"
  LogSet on
SectionEnd

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}" 
OutFile "WsaAssistant-Setup-arm64-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\WsaAssistant"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
  nsProcess::_KillProcess /NOUNLOAD "adb.exe"
  nsProcess::_KillProcess /NOUNLOAD "WsaAssistant.exe"
  nsProcess::_KillProcess /NOUNLOAD "WsaAssistant.Background.exe"
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  File "Publish\arm64\*.*" 
  SetOutPath "$INSTDIR\ru"
  File "Publish\arm64\ru\*.*" 
  SetOutPath "$INSTDIR\zh-Hans"  
  File "Publish\arm64\zh-Hans\*.*" 
  SetOutPath "$INSTDIR\zh-Hant"  
  File "Publish\arm64\zh-Hant\*.*" 
  
  CreateDirectory "$SMPROGRAMS\WsaAssistant"
  CreateShortCut "$SMPROGRAMS\WsaAssistant\WsaAssistant.lnk" "$INSTDIR\WsaAssistant.exe"
  CreateShortCut "$DESKTOP\WsaAssistant.lnk" "$INSTDIR\WsaAssistant.exe"
  File "Publish\arm64\AdbWinUsbApi.dll"
  File "Publish\arm64\AdbWinApi.dll"
  File "Publish\arm64\adb.exe"
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\WsaAssistant\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\WsaAssistant.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\WsaAssistant.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) 已成功地从你的计算机移除。"
FunctionEnd

Function un.onInit
  nsProcess::_KillProcess /NOUNLOAD "adb.exe"
  nsProcess::_KillProcess /NOUNLOAD "WsaAssistant.exe"
  nsProcess::_KillProcess /NOUNLOAD "WsaAssistant.Background.exe"
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "你确实要完全移除 $(^Name) ，其及所有的组件？" IDYES +2
  Abort
FunctionEnd


!macro DelFileByLog LogFile
  ifFileExists `${LogFile}` 0 +4
    Push `${LogFile}`
    Call un.DelFileByLog
    Delete `${LogFile}`
!macroend
 
Section Uninstall
  ; 调用宏只根据安装日志卸载安装程序自己安装过的文件
  !insertmacro DelFileByLog "$INSTDIR\install.log"
  
  Delete "$SMPROGRAMS\WsaAssistant\Uninstall.lnk"
  Delete "$DESKTOP\WsaAssistant.lnk"
  Delete "$SMPROGRAMS\WsaAssistant\WsaAssistant.lnk"

  RMDir "$SMPROGRAMS\WsaAssistant"
  RMDir "$INSTDIR\zh-Hant"
  RMDir "$INSTDIR\zh-Hans"
  RMDir "$INSTDIR\ru"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
  
SectionEnd
 

; 以下是卸载程序通过安装日志卸载文件的专用函数，请不要随意修改
  Function un.DelFileByLog
  Exch $R0
  Push $R1
  Push $R2
  Push $R3
  FileOpen $R0 $R0 r
  ${Do}
	FileRead $R0 $R1
	${IfThen} $R1 == `` ${|} ${ExitDo} ${|}
	StrCpy $R1 $R1 -2
	StrCpy $R2 $R1 11
	StrCpy $R3 $R1 20
	${If} $R2 == "File: wrote"
	${OrIf} $R2 == "File: skipp"
	${OrIf} $R3 == "CreateShortCut: out:"
	${OrIf} $R3 == "created uninstaller:"
	  Push $R1
	  Push `"`
	  Call un.DelFileByLog.StrLoc
	  Pop $R2
	  ${If} $R2 != ""
		IntOp $R2 $R2 + 1
		StrCpy $R3 $R1 "" $R2
		Push $R3
		Push `"`
		Call un.DelFileByLog.StrLoc
		Pop $R2
		${If} $R2 != ""
		  StrCpy $R3 $R3 $R2
		  Delete /REBOOTOK $R3
		${EndIf}
	  ${EndIf}
	${EndIf}
	StrCpy $R2 $R1 7
	${If} $R2 == "Rename:"
	  Push $R1
	  Push "->"
	  Call un.DelFileByLog.StrLoc
	  Pop $R2
	  ${If} $R2 != ""
		IntOp $R2 $R2 + 2
		StrCpy $R3 $R1 "" $R2
		Delete /REBOOTOK $R3
	  ${EndIf}
	${EndIf}
  ${Loop}
  FileClose $R0
  Pop $R3
  Pop $R2
  Pop $R1
  Pop $R0
FunctionEnd

Function un.DelFileByLog.StrLoc
  Exch $R0
  Exch
  Exch $R1
  Push $R2
  Push $R3
  Push $R4
  Push $R5
  StrLen $R2 $R0
  StrLen $R3 $R1
  StrCpy $R4 0
  ${Do}
	StrCpy $R5 $R1 $R2 $R4
	${If} $R5 == $R0
	${OrIf} $R4 = $R3
	  ${ExitDo}
	${EndIf}
	IntOp $R4 $R4 + 1
  ${Loop}
  ${If} $R4 = $R3
	StrCpy $R0 ""
  ${Else}
	StrCpy $R0 $R4
  ${EndIf}
  Pop $R5
  Pop $R4
  Pop $R3
  Pop $R2
  Pop $R1
  Exch $R0
FunctionEnd
